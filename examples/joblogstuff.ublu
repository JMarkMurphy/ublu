# joblogstuff.ublu
# Example from Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2016 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# Give an as400 a jobname, a jobuser and a jobnumber (the latter 3 all strings)
# print all messages in the joblog for the job. If 'verbose' is true, extended
# message info is displayed.
# E.g.,
#   as400 -to @mysys mysys myuid mypassword
#   tuple -true @tf
#   catJobLog ( @mysys MYDISPDEV MYUID 123654 @tf )
#   tuple -false @tf
#   catJobLog ( @mysys MYDISPDEV MYUID 123654 @tf )
FUNC catJobLog ( sys jobname jobuser jobnumber verbose ) $[
    LOCAL @jl
    joblog -to @jl -as400 @@sys -new @@jobname @@jobuser @@jobnumber
    joblog -to ~ -- @jl -length
    joblog -- @jl -to ~ -qm 0 ~
    FOR @i in ~ $[ 
        IF @@verbose THEN $[
            put -from @i
        ]$ ELSE $[
            msg -- @i -message
        ]$
    ]$
]$

# Get the reply status of a queued message. There's currently (2016-10-09)
# no direct Ublu dash-command to get this status. E.g., 
#   as400 -to @mysys SOMESYS.com myid MyPaSsWd
#   joblog -to @jl -as400 @mysys -new QPAD9999 MYID 349989
#   joblog -- @jl -to ~ -length
#   joblog -- @jl -to @qml -qm 0 ~
#   FOR @i in @qml $[ msg -- @i -message replyStatus ( @i ~ ) put ~ ]$
FUNC replyStatus ( queuedMessage status ) $[
    calljava -- @@queuedMessage -method getReplyStatus -to @@status
]$
