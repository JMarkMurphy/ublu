# t.record.ublu
# Component of test.all ( )
# Test Suite for Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2017 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# Assumes called from test.all.ublu so
# ... so defaults are already included
#
# include -s test_utils.ublu
# ... is already included from test.all.ublu
#
# Assumes the present file is residing in examples/test/suite 
include ../../../examples/listsrc.ublu

# t.record ( work_dir testsys testrptpf )
# ... work_dir - where tests can create files and leave them
# ... testsys - the server against which tests to be run
# ... testrptpf - open pf to write test report records
FUNC t.record ( work_dir testsys testrptpf ) $[
    LOCAL @FILNAM LOCAL @TMSTRT  LOCAL @TMEND LOCAL @DESC LOCAL @PFAIL
    LOCAL @result LOCAL @results
    LOCAL @tmpfile LOCAL @difflist
    
    put ${ -------------- }$
    put ${ Start t.record }$
    put ${ -------------- }$
    put ${ }$
        
    cat_fp ( @@work_dir t.record.listsrc.txt )
    put -to @tmpfile ~
    cat_fp ( *t_paradigm t.record.listsrc.txt )
    put -to @paradigm ~
    cat_fp ( @@work_dir t.record.listsrc.diff.txt )
    put -to @difflist ~

    map -to @result
    
    string -to @FILNAM -trim t.record.ublu
    @result -> @FILNAM
    
    string -to @DESC -cat ${ listsrc:diff in }$ @difflist
    @result -> @DESC
    
    currentTimeMillis ( )
    tuple -assign @TMSTRT ~
    @result -> @TMSTRT
    
    fqp_file ( *t_library *t_sourcepf )
    fqp_mbr ( ~ *t_sourcembr )
    const *t_record_source_mbr_fqp ~
    
    listsrcToLocalFile ( @@testsys *t_record_source_mbr_fqp @tmpfile )
    
    currentTimeMillis ( )
    tuple -assign @TMEND ~
    @result -> @TMEND
    
    string -to ~ -cat ${ diff -u }$ @tmpfile
    ~ -to ~ -bl
    ~ -to ~ -cat @paradigm
    
    \\ ${ debug }$ lifo -dup put ~
    
    system -to ~ ~
      
    lifo -dup
    calljava -to ~ -method getOutput -obj ~
    put -tofile @difflist ~
        
    calljava -to ~ -method getRc -obj ~
    eval -to ~ == 0 ~
    
    \\ ${ debug }$ lifo -dup put -n -s ${ result of diff }$ put ~
    
    IF ~ THEN $[ put -to @PFAIL P ]$ ELSE $[ put -to @PFAIL F ]$

    \\ ${ debug }$ put -n -s ${ value of PFAIL }$ put @PFAIL
      
    @result -> @PFAIL 
    
    writeMapToRecord ( @@testrptpf @result )
      
    put ${ ------------ }$    
    put ${ end t.record }$
    put ${ ------------ }$
    put ${ }$

]$