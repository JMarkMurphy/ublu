# t.programcall.ublu
# Component of test.all ( )
# Test Suite for Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2018 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# Assumes called from test.all.ublu so
# ... so defaults are already included
#
# include -s test_utils.ublu
# ... is already included from test.all.ublu
#
# Assumes the present file is residing in examples/test/suite
include -s ../../../examples/test/testprogramcall.ublu
include -s ../../../examples/test/testprogramcall1.ublu

# t.programcall ( work_dir testsys testrptpf )
# ... work_dir - where tests can create files and leave them
# ... testsys - the server against which tests to be run
# ... testrptpf - open pf to write test report records
FUNC t.programcall ( work_dir testsys testrptpf ) $[
    LOCAL @FILNAM LOCAL @TMSTRT LOCAL @TMEND LOCAL @DESCR LOCAL @PFAIL
    LOCAL @result LOCAL @results
    LOCAL @tmpfile LOCAL @paradigm LOCAL @difflist
    LOCAL @dateparmfqp LOCAL @testdate

    put ${ ------------------- }$
    put ${ Start t.programcall }$
    put ${ ------------------- }$
    put ${ }$

    put -to @dateparmfqp /QSYS.LIB/JWOEHR.LIB/DATEPARMSR.PGM
    put -to @testdate 10/31/2018

    cat_fp ( @@work_dir t.programcall.txt )
    put -to @tmpfile ~
    cat_fp ( *t_paradigm t.programcall.txt )
    put -to @paradigm ~
    cat_fp ( @@work_dir t.programcall.diff.txt )
    put -to @difflist ~

    map -to @result

    string -to @FILNAM -trim t.programcall.ublu
    @result -> @FILNAM

    string -to @DESCR -cat ${ testpcall:test programcall:diff in }$ @difflist
    @result -> @DESCR

    currentTimeMillis ( )
    tuple -assign @TMSTRT ~
    @result -> @TMSTRT

    \\ ${ Assumes the present file is residing in examples/test/suite }$
    redirall ( @tmpfile $[ testProgramCallSys ( @@testsys @dateparmfqp @testdate ) testprogramcall1 ( @@testsys ) ]$ )

    currentTimeMillis ( )
    tuple -assign @TMEND ~
    @result -> @TMEND

    string -to ~ -cat ${ diff -u }$ @tmpfile
    ~ -to ~ -bl
    ~ -to ~ -cat @paradigm

    \\ ${ announce }$
    put -n -s *** lifo -dup put -n -s ~

    system -to ~ ~

    lifo -dup

    calljava -to ~ -method getOutput -obj ~
    put -tofile @difflist ~

    calljava -to ~ -method getRc -obj ~

    \\ ${ announce }$
    lifo -dup put -n -s ~

    eval -to ~ == 0 ~

    IF ~ THEN $[ put -to @PFAIL P ]$ ELSE $[ put -to @PFAIL F ]$

    \\ ${ announce }$
    put @PFAIL

    @result -> @PFAIL

    writeMapToRecord ( @@testrptpf @result )

    put ${ ----------------- }$
    put ${ End t.programcall }$
    put ${ ----------------- }$
    put ${ }$

]$
