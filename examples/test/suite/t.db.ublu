# t.db.ublu
# Component of test.all ( )
# Test Suite for Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2018 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# Assumes called from test.all.ublu so
# ... so defaults are already included
#
# include -s test_utils.ublu
# ... is already included from test.all.ublu
#
# Assumes the present file is residing in examples/test/suite
include -s ../../../examples/test/testdbrepl.ublu

# t.db ( work_dir testsys testrptpf )
# ... work_dir - where tests can create files and leave them
# ... testsys - the server against which tests to be run
# ... testrptpf - open pf to write test report records
FUNC t.db ( work_dir testsys testrptpf ) $[
    LOCAL @FILNAM LOCAL @TMSTRT LOCAL @TMEND LOCAL @DESCR LOCAL @PFAIL
    LOCAL @result LOCAL @results
    LOCAL @tmpfile LOCAL @paradigm LOCAL @difflist

    LOCAL @destssl
    tuple -assign @destssl @pg_ssl
    LOCAL @destpasswd
    put -to @destpasswd *t_postgres_passwd
    LOCAL @destusr
    put -to @destusr *t_postgres_user
    LOCAL @destdb
    put -to @destdb *t_postgres_db
    LOCAL @destsys
    put -to @destsys *t_postgres_host
    LOCAL @srcssl
    tuple -assign @srcssl @ssl
    LOCAL @srctable
    put -to @srctable *t_repl_table
    LOCAL @srcpasswd
    put -to @srcpasswd *t_password
    LOCAL @srcusr
    put -to @srcusr *t_userprof
    LOCAL @srcschema
    put -to @srcschema *t_src_repl_schema
    LOCAL @srcsys
    put -to @srcsys *t_server

    put ${ ---------- }$
    put ${ Start t.db }$
    put ${ ---------- }$
    put ${ }$

    cat_fp ( @@work_dir t.db.txt )
    put -to @tmpfile ~
    cat_fp ( *t_paradigm t.db.txt )
    put -to @paradigm ~
    cat_fp ( @@work_dir t.db.diff.txt )
    put -to @difflist ~

    map -to @result

    string -to @FILNAM -trim t.db.ublu
    @result -> @FILNAM

    string -to @DESCR -cat ${ testdb:test table replication to PostgreSQL:diff in }$ @difflist
    @result -> @DESCR

    currentTimeMillis ( )
    tuple -assign @TMSTRT ~
    @result -> @TMSTRT

    \\ ${ Assumes the present file is residing in examples/test/suite }$
    redirall ( @tmpfile $[ testSuiteDbRepl ( @srcsys @srcschema @srcusr @srcpasswd @srctable @srcssl @destsys @destdb @destusr @destpasswd @destssl ) ]$ )

    currentTimeMillis ( )
    tuple -assign @TMEND ~
    @result -> @TMEND

    string -to ~ -cat ${ diff -u }$ @tmpfile
    ~ -to ~ -bl
    ~ -to ~ -cat @paradigm

    \\ ${ announce }$
    put -n -s *** lifo -dup put -n -s ~

    system -to ~ ~

    lifo -dup

    calljava -to ~ -method getOutput -obj ~
    put -tofile @difflist ~

    calljava -to ~ -method getRc -obj ~

    \\ ${ announce }$
    lifo -dup put -n -s ~

    eval -to ~ == 0 ~

    IF ~ THEN $[ put -to @PFAIL P ]$ ELSE $[ put -to @PFAIL F ]$

    \\ ${ announce }$
    put @PFAIL

    @result -> @PFAIL

    writeMapToRecord ( @@testrptpf @result )

    put ${ -------- }$
    put ${ End t.db }$
    put ${ -------- }$
    put ${ }$

]$
