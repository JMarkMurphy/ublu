# msgstuff.ublu
# Example from Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2016 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# Routines for utilizing message capabilities of host
# Jack Woehr 2014-06-03, 2016-10-09

# Convert system object and username to a message queue object
FUNC userToMsgQ ( as400 user mq ) $[
	LOCAL @u LOCAL @ifspath LOCAL @tst 
	string -to @@user -trim @@user
	string -to @tst -trim ${ }$
	test -to @tst -ne @tst @@user
	tuple -null @@mq
	IF @tst THEN $[
		user -as400 @@as400 -to @u -new -userprofile @@user
		user -user @u -to @ifspath -query msgq
		msgq -as400 @@as400 -new -to @@mq @ifspath
	]$
]$

# Send an informational message to a user
FUNC sendInfo ( as400 user text ) $[
	LOCAL @tst LOCAL @mq
	userToMsgQ ( @@as400 @@user @mq )
	test -to @tst -nnull @mq
	IF @tst THEN $[
		msgq -msgq @mq -sendinfo @@text
		msgq -msgq @mq -close
	]$ ELSE $[
		put ${ null message queue passed in sendInfo }$
	]$
]$

# Get messages for a user and leave a tuple instance with the
# still-open message queue.
FUNC getMsgs ( as400 user mq msgs ) $[
	userToMsgQ ( @@as400 @@user @@mq )
	msgq -msgq @@mq -query -to @@msgs
]$

# Send an informational message to a list of users
FUNC broadcastInfo ( as400 userlist text ) $[
	FOR @u in @@userlist $[
	sendInfo ( @@as400 @u @@text )
	]$
]$

