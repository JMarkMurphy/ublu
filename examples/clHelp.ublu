# clHelp.ublu
# Example from Ublu Midrange and Mainframe Life Cycle Extension language
# https://github.com/jwoehr/ublu
# Copyright (C) 2016 Jack J. Woehr http://www.softwoehr.com
# See the Ublu license (BSD-2 open source)

# clHelp ( lib cmd s u p ) 
# Generate HTML help for a CL command
# args are library commandname system user password
# example: clHelp ( QSYS WRKUSRPRF MY400 MYID ******** )
# a file is generated in the current directory QSYS_WRKUSRPRF.html
FUNC clHelp ( lib cmd s u p ) $[
    LOCAL @start LOCAL @end
    num -to ~ -int 10
    calljava -to ~ -forname java.lang.String
    calljava -to @L[String -class java.lang.reflect.Array -method newInstance -arg ~ -primarg ~ 
    string -to ~ -trim @@lib
    string -to ~ -trim ${ -l }$
    string -to ~ -trim @@cmd
    string -to ~ -trim ${ -c }$
    string -to ~ -trim @@s
    string -to ~ -trim ${ -s }$
    string -to ~ -trim @@u
    string -to ~ -trim ${ -u }$
    string -to ~ -trim @@p
    string -to ~ -trim ${ -p }$
    put -to @start 0
    put -to @end 10
    DO @start @end $[
        calljava  -class java.lang.reflect.Array -method set -arg @L[String -primarg @start -arg ~
    ]$
    calljava -class com.ibm.as400.util.CommandHelpRetriever -method main -arg @L[String
]$
  
# browseClHelp ( lib cmd s u p ) 
# Generate HTML help for a CL command and open default browser on it.
# args are library commandname system user password
# example: clHelp ( QSYS WRKUSRPRF MY400 MYID ******** )
# a file is generated in the current directory QSYS_WRKUSRPRF.html and it
# is loaded into the user's default browser.
FUNC browseClHelp ( lib cmd s u p ) $[
    LOCAL @filename
    put -n -to ~ ${ file:// }$
    ~ -to ~ -trim
    system -to ~ ${ pwd }$
    \\ ${ system returns an ublu.util.SystemHelper.ProcessClosure object.
    This calljava gets the output to concatenate to for the absolute URI. }$
    calljava -to ~ -- ~ -method getOutput
    lifo -swap
    ~ -to ~ -cat ~
    ~ -to ~ -cat /
    ~ -to ~ -cat @@lib
    ~ -to ~ -cat _
    ~ -to ~ -cat @@cmd
    ~ -to ~ -cat ${ .html }$
    ~ -to @filename -trim
    put @filename
    clHelp ( @@lib @@cmd @@s @@u @@p )
    desktop -browse @filename
]$
# end
